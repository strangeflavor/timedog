// 2018-09-20 22:18:25
#event create
#action action_inherited
#section
active = true;
invul = false;
dangerous = true;
killWhenBelowScreen = true;
direction_to_angle = false;
_rotAmount = 1;
flashing = 0;

if y > (vh + 32) instance_destroy();

image_xscale = scale * 2;
image_yscale = scale * 2;
image_speed = choose(.1,.25,-.2);
image_index = choose(0,1);

HP = 3;

make(Smoke);
with make(oExplo) {
    depth -= 1;
    image_blend = c_red;
}

#event destroy

if active {
    if y < vh + 32*scale {
        sfx(sndDefeatEnemy);
        with make(oExplo) {
            image_blend = c_yellow;
            direction = other.direction;
            speed = other.speed;
        }
    }
}

#event step
#action action_inherited
#section
if (live_call()) return live_result;

if killWhenBelowScreen if y > (vh + 32*scale) kill(self);

var _destinationDirection = direction;

if objectAge < 30 {
    _destinationDirection = point_direction(x, y, x, y+200);
} else {
    if objectAge < 70 and instance_exists(oShip) {
        _destinationDirection = point_direction(x, y, oShip.x, oShip.y);
    }
}

_rotAmount = lerp(_rotAmount,4,.25);
var prevDirection = direction;
direction += sign(dsin(_destinationDirection - direction)) * _rotAmount;
if speed < 9 speed += .2;

image_angle = prevDirection;


#event collision:oShot
if active and !invul and y > 0 {
    with other {
        HP -= 1;
        if HP <= 0 {
            kill();
            sfx(sndDamageEnemy)
        }
    }
    HP -= other.damage;
    if HP < 0 kill();
}

#event draw

if objectAge > 3 {
    if objectAge > 30 and objectAge < 90 {
        draw_sprite_ext(sMissileThruster,objectAge*.25,x,y,image_xscale,image_yscale,image_angle,c_yellow,1);
        smokeTrail();
    }
    
    if flashing < 0 {
        flashing += 1;
        if flashing == 0 flashing = 12;
    } else {
        if flashing > 0 flashing -= 1;
    }
    
    draw_self();
    
    if flashing < 0 {
        draw_sprite_ext(sprite_index,image_index,x,y,image_xscale,image_yscale,image_angle,c_red,.7);
    }
}